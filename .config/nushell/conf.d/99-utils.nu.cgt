def & [...argv] {
    job spawn { ^($argv | get 0) ...($argv | skip 1) }
}

def --env mkdircd [p: string] {
    mkdir $p
    cd $p
}

def wldd [path: string] {
    match (which $path) {
        [{ path: $p }] => { ldd $p },
        _ => { error make { msg: $"No external command ($path)" } },
    }
}

def viewdoc [f: path] {
    let inp = $f | path parse

    match $inp.extension {
        $e if $e in [ "doc", "docx", "odt", "ppt", "pptx", "odp" ] => {
            let outp = { extension: "pdf", parent: "/tmp", stem: $inp.stem } | path join
            libreoffice --convert-to pdf $f --outdir /tmp
            try { zathura $outp }
            rm $outp
        },
        "pdf" => { zathura $f },
        "html" => { openbrowser $"file://($f | path expand)" }
        $e if $e in [ "png", "webp", "jpg", "jpeg", "ff", "bmp" ] => { nsxiv $f },
        _ => { mpv $f },
    }
}

def pgrep [pattern: string, --all(-a)]: nothing -> table<pid: int, ppid: int, name: string, status: string, cpu: float, mem: filesize, virtual: filesize> {
    if $all { ps -l } else { ps } | where name =~ $pattern
}

# A wrapper around yt-dlp that passes some sane defaults and limits resolution.
def --wrapped viddl [
  url: string,
  --output (-o): string = "%(uploader)s - %(title)s.%(ext)s",
  --format (-f): string = "bestvideo[height<=<% opt.videosize[2] %>]+bestaudio/best[height<=<% opt.videosize[2] %>]",
  ...args
] {
    (yt-dlp $url
    --sponsorblock-mark all
    <! if opt.good_internet then !>--limit-rate (1024 * 1024) # needed to not get blocked
    <! end !>--format $format
    --output $output
    --embed-metadata
    ...$args)
}
